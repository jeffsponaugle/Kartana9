Name	 K9MEMCD;
PartNo   K9MEMCD;
Date     04/18/2022;
Revision 01;
Designer Sponaugle;
Company  Ratiometric;
Assembly None;
Location None;
Device   f1504ispplcc44;
      

PROPERTY ATMEL {TDI_PULLUP = ON};
PROPERTY ATMEL {TMS_PULLUP = ON};
PROPERTY ATMEL {PREASSIGN = KEEP};
PROPERTY ATMEL {XOR_SYNTHESIS = OFF};

/* Input Pins 43,44,1,2*/

Pin[1] = MEM_AS;
Pin[2] = MEM_RW;
Pin[43] = MEM_L_EN;
Pin[44] = MEM_H_EN;
Pin[16,17,18,19,20,21,24,25,26,27,28,29,31,33,34] = [A1..15];

Pin[41] = RAM_HIGH_CS;
Pin[40] = RAM_LOW_CS;
Pin[39] = RAM_OE;
Pin[37] = RAM_WE;
Pin[36] = ROM_HIGH_CS;
Pin[4] = ROM_LOW_CS;
Pin[5] = ROM_OE;
Pin[6] = ROM_WE;

Pin[8] = SW_LOW_READ;
Pin[9] = SW_HIGH_READ;
Pin[11] = SEVEN_LOW_WRITE;
Pin[12] = SEVEN_HIGH_WRITE;
Pin[14] = UNUSED;

UNUSED = A1#A2#A3#A4;

FIELD addr = [A1..15];

/* Memory Map 

    ROM 0x0000 - 0x7FFF
    RAM 0x8000 - 0xFFF0

*/

ROM_ACCESS = (!MEM_AS & addr:['b'0XXXXXXXXXXXXXXX]);
RAM_ACCESS = (!MEM_AS & ( addr:['b'1XXXXXXXXXXXXXXX] & !(addr:['b'111111111111XXXX])));

SW_LOW_READ =  (!MEM_AS & addr:['h'FFF0] & MEM_RW & !MEM_L_EN);
SW_HIGH_READ = (!MEM_AS & addr:['h'FFF0] & MEM_RW & !MEM_H_EN);
SEVEN_LOW_WRITE = (!MEM_AS & addr:['h'FFF0] & !MEM_RW & !MEM_L_EN);
SEVEN_HIGH_WRITE = (!MEM_AS & addr:['h'FFF0] & !MEM_RW & !MEM_H_EN);


RAM_HIGH_CS = !( RAM_ACCESS & !MEM_H_EN
                );

RAM_LOW_CS = !( RAM_ACCESS & !MEM_L_EN
                );                

ROM_HIGH_CS = !( ROM_ACCESS & !MEM_H_EN
                );

ROM_LOW_CS = !( ROM_ACCESS & !MEM_L_EN
                );  

RAM_OE = !( RAM_ACCESS & MEM_RW
                );

RAM_WE = !( RAM_ACCESS & !MEM_RW
                );

ROM_OE = !( ROM_ACCESS & MEM_RW
                );

ROM_WE = !( ROM_ACCESS & !MEM_RW
                );

